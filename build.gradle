buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.3"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'org/codet/caseidilla/config/**')
        })
    }
}

group = 'org.codet'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}


dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation("org.springframework.boot:spring-boot-starter-websocket")
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('javax.xml.bind:jaxb-api:2.3.0')
    implementation('com.h2database:h2')
    implementation ('org.projectlombok:lombok:1.18.2')
    implementation('com.google.collections:google-collections:1.0-rc2')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jdk8')
    implementation('com.fasterxml.jackson.module:jackson-module-parameter-names')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.7')


    testImplementation('io.rest-assured:rest-assured:3.0.0')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.hamcrest:java-hamcrest:2.0.0.0')
}
